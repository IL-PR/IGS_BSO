
#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт 
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке(СтандартныеПодсистемыСервер.ВерсияБиблиотеки());	
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиПечатнаяФорма();
	ПараметрыРегистрации.Наименование = "Печать КС-2";
	ПараметрыРегистрации.Версия = "1.0";
	ПараметрыРегистрации.Информация = "Внешняя печатная форма КС-2 для документа ""Реализация строительных работ и услуг"". Задача № 0145 Профрешение";
	ПараметрыРегистрации.БезопасныйРежим = Ложь; 
	ПараметрыРегистрации.Назначение = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве("Документ.ИмпРеализацияСтроительныхРаботУслуг");
	ДобавитьКоманду(ПараметрыРегистрации.Команды, "Печать КС-2", "ПЕЧАТЬКС2", ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовСерверногоМетода(), Ложь, "ПечатьMXL"); 
	
	Возврат ПараметрыРегистрации;
	
КонецФункции 

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ТабличныйДокументПечатьКС2 = ПечатьКС2БСО(МассивОбъектов); 
	
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПЕЧАТЬКС2", Нстр("ru = 'Печать КС-2'"), ТабличныйДокументПечатьКС2); 
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	
	НоваяКоманда = ТаблицаКоманд.Добавить();
	НоваяКоманда.Представление = Представление;
	НоваяКоманда.Идентификатор = Идентификатор;	
	НоваяКоманда.Использование = Использование;	
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение;
	НоваяКоманда.Модификатор = Модификатор;
	
КонецПроцедуры

Функция ПечатьКС2БСО(МассивОбъектов)
	
	ТабДокумент = Новый ТабличныйДокумент;
	Макет = ПолучитьМакет("КС2");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(НоменклатурныеГруппы.Родитель) КАК Объект,
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка КАК ДокументСсылка,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(НоменклатурныеГруппы.Родитель.Родитель.Родитель) КАК Стройка
	|ПОМЕСТИТЬ ВТ_ДанныеПоСтройкамИОбъектам
	|ИЗ
	|	Документ.ИмпРеализацияСтроительныхРаботУслуг.Услуги КАК ИмпРеализацияСтроительныхРаботУслугУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
	|		ПО ИмпРеализацияСтроительныхРаботУслугУслуги.Субконто = НоменклатурныеГруппы.Ссылка
	|ГДЕ
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка В (&МассивОбъектов)
	|	И ИмпРеализацияСтроительныхРаботУслугУслуги.НомерСтроки = 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИмпРеализацияСтроительныхРаботУслуг.Дата КАК ДокументДата,
	|	ИмпРеализацияСтроительныхРаботУслуг.Номер КАК ДокументНомер,
	|	ИмпРеализацияСтроительныхРаботУслуг.Инвестор КАК Инвестор,
	|	ДоговорыКонтрагентов.Номер КАК ДоговорНомер,
	|	ДоговорыКонтрагентов.Дата КАК ДоговорДата,
	|	ИмпРеализацияСтроительныхРаботУслуг.Организация КАК Подрядчик,
	|	ИмпРеализацияСтроительныхРаботУслуг.Контрагент КАК Заказчик,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(СправочникНоменклатура.ЕдиницаИзмерения) КАК БазоваяЕдИзм,
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Содержание КАК ТоварНаименование,
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Сумма КАК Сумма,
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.СуммаНДС КАК СуммаНДС,
	|	ИмпРеализацияСтроительныхРаботУслугУслуги.Количество КАК Количество,
	|	ВЫБОР
	|		КОГДА ИмпРеализацияСтроительныхРаботУслуг.СуммаВключаетНДС
	|			ТОГДА ВЫБОР
	|					КОГДА ИмпРеализацияСтроительныхРаботУслугУслуги.Количество > 0
	|						ТОГДА ИмпРеализацияСтроительныхРаботУслугУслуги.Цена - ИмпРеализацияСтроительныхРаботУслугУслуги.СуммаНДС / ИмпРеализацияСтроительныхРаботУслугУслуги.Количество
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ИмпРеализацияСтроительныхРаботУслугУслуги.Цена
	|	КОНЕЦ КАК ЦенаБезНДС,
	|	ВЫБОР
	|		КОГДА ИмпРеализацияСтроительныхРаботУслуг.СуммаВключаетНДС
	|			ТОГДА ИмпРеализацияСтроительныхРаботУслугУслуги.Сумма - ИмпРеализацияСтроительныхРаботУслугУслуги.СуммаНДС
	|		ИНАЧЕ ИмпРеализацияСтроительныхРаботУслугУслуги.Сумма
	|	КОНЕЦ КАК СуммаБезНДС,
	|	ВЫБОР
	|		КОГДА ИмпРеализацияСтроительныхРаботУслуг.СуммаВключаетНДС
	|			ТОГДА ИмпРеализацияСтроительныхРаботУслугУслуги.Сумма
	|		ИНАЧЕ ИмпРеализацияСтроительныхРаботУслугУслуги.Сумма + ИмпРеализацияСтроительныхРаботУслугУслуги.СуммаНДС
	|	КОНЕЦ КАК СуммаСНДС,
	|	ИмпРеализацияСтроительныхРаботУслуг.Руководитель КАК Руководитель,
	|	ИмпРеализацияСтроительныхРаботУслуг.Ссылка КАК ДокументСсылка,
	|	ЕСТЬNULL(ВТ_ДанныеПоСтройкамИОбъектам.Объект, """") КАК Объект,
	|	ЕСТЬNULL(ВТ_ДанныеПоСтройкамИОбъектам.Стройка, """") КАК Стройка,
	|	ИмпРеализацияСтроительныхРаботУслуг.ЗаРуководителяНаОсновании КАК Доверенность
	|ПОМЕСТИТЬ ВТ_ОсновныеДанные
	|ИЗ
	|	Документ.ИмпРеализацияСтроительныхРаботУслуг.Услуги КАК ИмпРеализацияСтроительныхРаботУслугУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ИмпРеализацияСтроительныхРаботУслуг КАК ИмпРеализацияСтроительныхРаботУслуг
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|			ПО ИмпРеализацияСтроительныхРаботУслуг.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеПоСтройкамИОбъектам КАК ВТ_ДанныеПоСтройкамИОбъектам
	|			ПО ИмпРеализацияСтроительныхРаботУслуг.Ссылка = ВТ_ДанныеПоСтройкамИОбъектам.ДокументСсылка
	|		ПО ИмпРеализацияСтроительныхРаботУслугУслуги.Ссылка = ИмпРеализацияСтроительныхРаботУслуг.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ИмпРеализацияСтроительныхРаботУслугУслуги.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ИмпРеализацияСтроительныхРаботУслуг.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ОсновныеДанные.Подрядчик КАК Подрядчик,
	|	ВТ_ОсновныеДанные.ДокументДата КАК ДокументДата,
	|	ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка) КАК Должность,
	|	ВТ_ОсновныеДанные.Руководитель КАК Руководитель,
	|	ВТ_ОсновныеДанные.Доверенность КАК Доверенность
	|ИЗ
	|	ВТ_ОсновныеДанные КАК ВТ_ОсновныеДанные";
	
	ДанныеПодрядчика = Запрос.Выполнить().Выгрузить(); 
	
	Для Каждого Строка Из ДанныеПодрядчика Цикл 
		
		ДанныеРуководителя = ОбщегоНазначенияБПВызовСервера.ДанныеФизЛица(Строка.Подрядчик, Строка.Руководитель, Строка.ДокументДата);
		Строка.Должность = ДанныеРуководителя.Должность;
		
	КонецЦикла; 
	
	Запрос.УстановитьПараметр("ДанныеПодрядчика", ДанныеПодрядчика);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ДанныеПодрядчика.Руководитель КАК Руководитель,
	|	ДанныеПодрядчика.Подрядчик КАК Подрядчик,
	|	ДанныеПодрядчика.ДокументДата КАК ДокументДата,
	|	ДанныеПодрядчика.Должность КАК Должность,
	|	ДанныеПодрядчика.Доверенность КАК Доверенность
	|ПОМЕСТИТЬ ВТ_ДанныеПодрядчика
	|ИЗ
	|	&ДанныеПодрядчика КАК ДанныеПодрядчика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеПодрядчика.Руководитель КАК Руководитель,
	|	ВТ_ДанныеПодрядчика.Подрядчик КАК Подрядчик,
	|	ВТ_ДанныеПодрядчика.ДокументДата КАК ДокументДата,
	|	СправочникФизическиеЛица.ФИО КАК ФИОРуководителя,
	|	СправочникДолжности.Представление КАК ДолжностьРуководителя,
	|	ОснованияПраваПодписи.Представление КАК Доверенность
	|ПОМЕСТИТЬ ВТ_ДанныеПодвала
	|ИЗ
	|	ВТ_ДанныеПодрядчика КАК ВТ_ДанныеПодрядчика
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК СправочникФизическиеЛица
	|		ПО ВТ_ДанныеПодрядчика.Руководитель = СправочникФизическиеЛица.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Должности КАК СправочникДолжности
	|		ПО ВТ_ДанныеПодрядчика.Должность = СправочникДолжности.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОснованияПраваПодписи КАК ОснованияПраваПодписи
	|		ПО ВТ_ДанныеПодрядчика.Доверенность = ОснованияПраваПодписи.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОсновныеДанные.Стройка КАК Стройка,
	|	ВТ_ОсновныеДанные.Объект КАК Объект,
	|	ВТ_ОсновныеДанные.ДокументДата КАК ДокументДата,
	|	ВТ_ОсновныеДанные.ДокументНомер КАК ДокументНомер,
	|	ВТ_ОсновныеДанные.Инвестор КАК ИнвесторСсылка,
	|	ВТ_ОсновныеДанные.ДоговорНомер КАК ДоговорНомер,
	|	ВТ_ОсновныеДанные.ДоговорДата КАК ДоговорДата,
	|	ВТ_ОсновныеДанные.Подрядчик КАК ПодрядчикСсылка,
	|	ВТ_ОсновныеДанные.Заказчик КАК ЗаказчикСсылка,
	|	ВТ_ОсновныеДанные.БазоваяЕдИзм КАК БазоваяЕдИзм,
	|	ВТ_ОсновныеДанные.ТоварНаименование КАК ТоварНаименование,
	|	ВТ_ОсновныеДанные.Количество КАК Количество,
	|	ВТ_ОсновныеДанные.ЦенаБезНДС КАК ЦенаБезНДС,
	|	ВТ_ОсновныеДанные.СуммаСНДС КАК СуммаСНДС,
	|	ВТ_ОсновныеДанные.СуммаБезНДС КАК СуммаБезНДС,
	|	ВТ_ОсновныеДанные.ДокументСсылка КАК ДокументСсылка,
	|	ВТ_ДанныеПодвала.ФИОРуководителя КАК ФИОРуководителя,
	|	ВТ_ДанныеПодвала.ДолжностьРуководителя КАК ДолжностьРуководителя,
	|	ВТ_ДанныеПодвала.Доверенность КАК Доверенность
	|ИЗ
	|	ВТ_ОсновныеДанные КАК ВТ_ОсновныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеПодвала КАК ВТ_ДанныеПодвала
	|		ПО ВТ_ОсновныеДанные.ДокументДата = ВТ_ДанныеПодвала.ДокументДата
	|			И ВТ_ОсновныеДанные.Подрядчик = ВТ_ДанныеПодвала.Подрядчик
	|			И ВТ_ОсновныеДанные.Руководитель = ВТ_ДанныеПодвала.Руководитель
	|ИТОГИ
	|	МАКСИМУМ(Стройка),
	|	МАКСИМУМ(Объект),
	|	МАКСИМУМ(ДокументДата),
	|	МАКСИМУМ(ДокументНомер),
	|	МАКСИМУМ(ИнвесторСсылка),
	|	МАКСИМУМ(ДоговорНомер),
	|	МАКСИМУМ(ДоговорДата),
	|	МАКСИМУМ(ПодрядчикСсылка),
	|	МАКСИМУМ(ЗаказчикСсылка),
	|	СУММА(СуммаСНДС),
	|	СУММА(СуммаБезНДС),
	|	МАКСИМУМ(ФИОРуководителя),
	|	МАКСИМУМ(ДолжностьРуководителя),
	|	МАКСИМУМ(Доверенность)
	|ПО
	|	ДокументСсылка"; 
	
	ВыборкаДокумент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаДокумент.Следующий() Цикл
		
		ДанныеШапки = Новый Структура("Инвестор,Заказчик,Подрядчик,Стройка,Объект,ДокументНомер,ДокументДата,ДоговорНомер,ДоговорДата,ДатаС,ДатаДо");
		ДанныеПодвала = Новый Структура("ДолжностьРуководителя,ФИОРуководителя,Доверенность,ИтогоБезНДС,ИтогСуммыСНДС");
		
		ЗаполнитьЗначенияСвойств(ДанныеШапки, ВыборкаДокумент,, "ДокументДата,ДоговорДата");
		ДанныеШапки.ДатаС = Формат(НачалоМесяца(ВыборкаДокумент.ДокументДата), "ДЛФ=Д");
		ДанныеШапки.ДатаДо = Формат(КонецМесяца(ВыборкаДокумент.ДокументДата), "ДЛФ=Д");
		ДанныеШапки.ДокументДата = Формат(ВыборкаДокумент.ДокументДата, "ДЛФ=Д");
		ДанныеШапки.ДоговорДата = Формат(ВыборкаДокумент.ДоговорДата, "ДЛФ=Д"); 
		
		СведенияОИнвесторе = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаДокумент.ИнвесторСсылка, ВыборкаДокумент.ДокументДата);
		СведенияОПодрядчике = БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаДокумент.ПодрядчикСсылка, ВыборкаДокумент.ДокументДата);
		СведенияОЗаказчике =  БухгалтерскийУчетПереопределяемый.СведенияОЮрФизЛице(ВыборкаДокумент.ЗаказчикСсылка, ВыборкаДокумент.ДокументДата);
		
		ДанныеПредставления = "ПолноеНаименование,ЮридическийАдрес,Телефоны,Факс";
		
		ДанныеШапки.Инвестор = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОИнвесторе, ДанныеПредставления);
		ДанныеШапки.Подрядчик = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОПодрядчике, ДанныеПредставления);
		ДанныеШапки.Заказчик = ОбщегоНазначенияБПВызовСервера.ОписаниеОрганизации(СведенияОЗаказчике, ДанныеПредставления);
			
		ЗаполнитьЗначенияСвойств(ДанныеПодвала, ВыборкаДокумент);
		ДанныеПодвала.ИтогоБезНДС = ВыборкаДокумент.СуммаБезНДС;
		ДанныеПодвала.ИтогСуммыСНДС = ВыборкаДокумент.СуммаСНДС;
		
		ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
		ОбластьШапка.Параметры.Заполнить(ДанныеШапки);
		ТабДокумент.Вывести(ОбластьШапка);
		
		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаб");
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		
		Выборка = ВыборкаДокумент.Выбрать();
		НомерСтроки = 1;
		Пока Выборка.Следующий() Цикл
			ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
			ОбластьСтрока.Параметры.Заполнить(Выборка);
			ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
			ТабДокумент.Вывести(ОбластьСтрока);
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла; 
		
		ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
		ОбластьПодвал.Параметры.Заполнить(ДанныеПодвала);
		ТабДокумент.Вывести(ОбластьПодвал);
		
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		
	КонецЦикла;
	
	Возврат ТабДокумент; 
	
КонецФункции

#КонецОбласти 



	