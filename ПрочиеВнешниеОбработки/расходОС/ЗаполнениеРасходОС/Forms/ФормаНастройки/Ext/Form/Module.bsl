
&НаСервере
Процедура СформироватьНаСервере() 
	
	ЦелевойОбъект = Объект.СсылкаНаОбъект.ПолучитьОбъект();
	ЦелевойОбъект.РасходОС.Очистить();	
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
		|	игсАрендованныеОСОстатки.ОсновноеСредство КАК ОсновноеСредство,
		|	игсАрендованныеОСОстатки.Контрагент КАК Контрагент,
		|	игсАрендованныеОСОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	игсАрендованныеОСОстатки.Склад КАК Склад,
		|	игсАрендованныеОСОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.игсАрендованныеОС.Остатки(
		|			&Дата,
		|			ДоговорКонтрагента = &Договор
		|				И Контрагент = &Контрагент
		|				И Организация = &Организация
		|				И Склад = &Склад) КАК игсАрендованныеОСОстатки"; 
	
	Если НЕ ЗначениеЗаполнено(Объект.Договор) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоговорКонтрагента = &Договор","Истина");
	Иначе
		Запрос.УстановитьПараметр("Договор", Объект.Договор);
	КонецЕсли;    
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Контрагент = &Контрагент","Истина");
	Иначе
		Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация","Истина");
	Иначе
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Объект.Склад) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Склад = &Склад","Истина");
	Иначе
		Запрос.УстановитьПараметр("Склад", Объект.Склад);
	КонецЕсли; 
	
	Если Объект.Дата = Дата('00010101') Тогда
		Запрос.УстановитьПараметр("Дата", Новый Граница(ТекущаяДатаСеанса(), ВидГраницы.Включая));
	Иначе
		Запрос.УстановитьПараметр("Дата", Новый Граница(Объект.Дата, ВидГраницы.Включая));
	КонецЕсли;
	
	Результат = Запрос.Выполнить();  
	
	Если Не Результат.Пустой() Тогда 
		
		ОСДляЗаполнения = Результат.Выгрузить();
		
		Для Каждого ОС Из ОСДляЗаполнения Цикл   
			
			НоваяСтрока 					= ЦелевойОбъект.РасходОс.Добавить();
			НоваяСтрока.ОсновноеСредство 	= ОС.ОсновноеСредство;
			НоваяСтрока.Контрагент 			= ОС.Контрагент;
			НоваяСтрока.ДоговорКонтрагента 	= ОС.ДоговорКонтрагента;
			НоваяСтрока.Склад 				= ОС.Склад;
			НоваяСтрока.Сумма 				= ОС.СуммаОстаток;  
			
		КонецЦикла;   
		
	КонецЕсли;  
	
	ЦелевойОбъект.Записать();
	  
КонецПроцедуры

&НаКлиенте
Процедура Сформировать(Команда)
	
	СформироватьНаСервере(); 
	
	Если ВладелецФормы <> Неопределено Тогда
		ВладелецФормы.Прочитать();
	КонецЕсли;
	
	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)   
	
	ПолучитьДанныеИсходногоДокумента(); 
	
КонецПроцедуры 

&НаСервере
Процедура ПолучитьДанныеИсходногоДокумента()  
	
	ИсходныйОбъект 		= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(объект.СсылкаНаОбъект,"Контрагент, Организация, Дата",Истина,); 
	
	Объект.Организация  = ИсходныйОбъект.Организация;
	Объект.Дата 		= ИсходныйОбъект.Дата; 
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  
	
	Для Каждого Строка Из Параметры.ОбъектыНазначения Цикл
		
		Объект.СсылкаНаОбъект = Строка.Ссылка;
		
	КонецЦикла;
	
КонецПроцедуры

