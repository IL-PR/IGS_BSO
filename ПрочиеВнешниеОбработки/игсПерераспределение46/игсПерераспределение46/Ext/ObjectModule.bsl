Перем Контрагент;
Перем Договор;
Перем НоменклатурнаяГруппа;
Перем СуммаОборотДтБУ;
Перем СуммаОборотКтБУ;
Перем СуммаОборотДтНУ;

#Область ПрограммныйИнтерфейс

Функция СведенияОВнешнейОбработке() Экспорт
	
	// Заполнение ПараметровРегистрации
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.2.2.1");
	ПараметрыРегистрации.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка();
	ПараметрыРегистрации.БезопасныйРежим = Истина;
	ПараметрыРегистрации.Наименование = "Перераспределение Счета №46"; 
	ПараметрыРегистрации.Версия = "1.1";
	ПараметрыРегистрации.Информация = "Обработка позволяет перераспределять средства внутри выполненных проектов,
	|используя документ ""Операции, введенны вручную"".
	|
	|Автор: <ИГС> ПР Потылицын Г.С. #0142, 31.07.2025";
	
	// Добавление команды 
	НоваяКоманда = ПараметрыРегистрации.Команды.Добавить();
	НоваяКоманда.Представление = "Открыть форму";
	НоваяКоманда.Идентификатор = НоваяКоманда.Представление;
	НоваяКоманда.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СоздатьИзменитьОперацияБух() Экспорт
	
	// Используемые счета
	Счет46		= ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ВыполненныеЭтапыПоНезавершеннымРаботам");
	Счет90011	= ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ВыручкаНеЕНВД"); 
	
	// Планы счетов 
	ВыполненныеЭтапы = ПланыСчетов.Хозрасчетный.ВыполненныеЭтапыПоНезавершеннымРаботам;
	ВыручкаНеОблЕНВД = ПланыСчетов.Хозрасчетный.ВыручкаНеЕНВД;
	
	// НДС
	НДС18 = Перечисления.СтавкиНДС.НДС18;
	
	// Аргументы в структуре для формирования проводок
	СтруктураЗначений = Новый Структура;
	СтруктураЗначений.Вставить("ВыполненныеЭтапы",	ВыполненныеЭтапы);
	СтруктураЗначений.Вставить("ВыручкаНеОблЕНВД",	ВыручкаНеОблЕНВД);
	СтруктураЗначений.Вставить("НДС18",				НДС18);
	СтруктураЗначений.Вставить("Содержание",		"Перераспределение 46 счета");
	
	// Определение объекта ОперацииБух
	Если ЗначениеЗаполнено(ОперацияБух) Тогда 
		
		ДокументОперацияБух = ОперацияБух.ПолучитьОбъект();
		
		Дата = ОперацияБух.Дата;
		Организация = ОперацияБух.Организация;
		
		// Очистка ВСЕХ движений по этому документу
		ОбменДаннымиСервер.УдалитьДвиженияУДокумента(ДокументОперацияБух);
		
	Иначе 
		
		ДокументОперацияБух = Документы.ОперацияБух.СоздатьДокумент();
		
		ДокументОперацияБух.Дата = Дата;
		ДокументОперацияБух.Организация = Организация;
		
	КонецЕсли;
	
	ДокументОперацияБух.Содержание = СтруктураЗначений.Содержание;
	
	Ссылка = ДокументОперацияБух.Ссылка;
	
	Хозрасчетный = ДокументОперацияБух.Движения.Хозрасчетный;
	Хозрасчетный.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	// <ИГС> ПР Потылицын Г.С. #0142 09.10.25 {
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ХозрасчетныйОбороты.Субконто1 КАК Контрагент,
	|	ХозрасчетныйОбороты.Субконто2 КАК Договор,
	|	ХозрасчетныйОбороты.Субконто3 КАК НоменклатурнаяГруппа,
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотДт) КАК СуммаОборотДтБУ,
	|	СУММА(ХозрасчетныйОбороты.СуммаОборотКт) КАК СуммаОборотКтБУ,
	|	ХозрасчетныйОбороты.СуммаНУОборотДт КАК СуммаОборотДтНУ,
	|	ХозрасчетныйОбороты.СуммаНУОборотКт КАК СуммаОборотКтНУ
	|ПОМЕСТИТЬ ХозОбороты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(
	|			&НачалоПериода,
	|			&КонецПериода,
	|			,
	|			Счет = &Счет46,
	|			,
	|			Организация = &Организация
	|				И Субконто3 <> ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка),
	|			,
	|			) КАК ХозрасчетныйОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.Субконто2,
	|	ХозрасчетныйОбороты.Субконто3,
	|	ХозрасчетныйОбороты.СуммаНУОборотДт,
	|	ХозрасчетныйОбороты.СуммаНУОборотКт
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НоменклатурныеГруппы.Родитель КАК Проект,
	|	ХозОбороты.Контрагент КАК Контрагент,
	|	ХозОбороты.Договор КАК Договор,
	|	ХозОбороты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	СУММА(ХозОбороты.СуммаОборотДтБУ) КАК СуммаОборотДтБУ,
	|	СУММА(ХозОбороты.СуммаОборотКтБУ) КАК СуммаОборотКтБУ,
	|	ХозОбороты.СуммаОборотДтНУ КАК СуммаОборотДтНУ,
	|	ХозОбороты.СуммаОборотКтНУ КАК СуммаОборотКтНУ
	|ПОМЕСТИТЬ Итог
	|ИЗ
	|	Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ХозОбороты КАК ХозОбороты
	|		ПО (ХозОбороты.НоменклатурнаяГруппа = НоменклатурныеГруппы.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатурныеГруппы.Родитель,
	|	ХозОбороты.Контрагент,
	|	ХозОбороты.Договор,
	|	ХозОбороты.НоменклатурнаяГруппа,
	|	ХозОбороты.СуммаОборотДтНУ,
	|	ХозОбороты.СуммаОборотКтНУ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Итог.Проект КАК Проект,
	|	Итог.Контрагент КАК Контрагент,
	|	Итог.Договор КАК Договор,
	|	Итог.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	Итог.СуммаОборотДтБУ КАК СуммаОборотДтБУ,
	|	Итог.СуммаОборотКтБУ КАК СуммаОборотКтБУ,
	|	Итог.СуммаОборотДтНУ КАК СуммаОборотДтНУ,
	|	Итог.СуммаОборотКтНУ КАК СуммаОборотКтНУ
	|ИЗ
	|	Итог КАК Итог
	|ИТОГИ
	|	СУММА(СуммаОборотДтБУ),
	|	СУММА(СуммаОборотКтБУ),
	|	СУММА(СуммаОборотДтНУ),
	|	СУММА(СуммаОборотКтНУ)
	|ПО
	|	Контрагент,
	|	Договор,
	|	Проект"; 
	// } </ИГС>
	
	// Параметры подтягиваются из документа или из обработки
	Запрос.УстановитьПараметр("НачалоПериода", НачалоГода(Дата));
	Запрос.УстановитьПараметр("КонецПериода", Дата);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Счет46", Счет46);
	
	// <ИГС> ПР Потылицын Г.С. #0142 18.08.25 {
	ВыборкаКонтрагент = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаКонтрагент.Следующий() Цикл
		ВыборкаДоговор = ВыборкаКонтрагент.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДоговор.Следующий() Цикл
			
			ВыборкаПроект = ВыборкаДоговор.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПроект.Следующий() Цикл;
				Выборка = ВыборкаПроект.Выбрать();
				СуммаДтБУ = ВыборкаПроект.СуммаОборотДтБУ;
				СуммаКтБУ = ВыборкаПроект.СуммаОборотКтБУ;

				Если СуммаКтБУ = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				Пока Выборка.Следующий() Цикл
					Если Выборка.СуммаОборотДтБУ < Выборка.СуммаОборотКтБУ Тогда
						РазницаМеждуДтКт = Выборка.СуммаОборотКтБУ - Выборка.СуммаОборотДтБУ;
						Если СуммаДтБУ >= (Выборка.СуммаОборотДтБУ + РазницаМеждуДтКт) Тогда
							
							ЗаписьПеременных(Выборка);
							СуммаДтБУ = СуммаДтБУ - (Выборка.СуммаОборотДтБУ + РазницаМеждуДтКт);
							СформироватьПроводку(ДокументОперацияБух, Выборка, РазницаМеждуДтКт, 
							- Выборка.СуммаОборотДтНУ, Выборка.СуммаОборотДтБУ + РазницаМеждуДтКт - Выборка.СуммаОборотКтБУ, СтруктураЗначений);			
						ИначеЕсли СуммаДтБУ = 0 Тогда
							
							СформироватьПроводку(ДокументОперацияБух, Выборка, - Выборка.СуммаОборотДтБУ,
							- Выборка.СуммаОборотДтНУ, - Выборка.СуммаОборотКтБУ, СтруктураЗначений);
							ЗаписьПеременных(Выборка);
							
						Иначе
							Если СуммаДтБУ <= Выборка.СуммаОборотДтБУ Тогда	
								СформироватьПроводку(ДокументОперацияБух, Выборка, СуммаДтБУ - Выборка.СуммаОборотДтБУ,
								- Выборка.СуммаОборотДтНУ, Выборка.СуммаОборотДтБУ + СуммаДтБУ - Выборка.СуммаОборотДтБУ - Выборка.СуммаОборотКтБУ,
								СтруктураЗначений);
								ЗаписьПеременных(Выборка);
								СуммаДтБУ = 0;
							Иначе
								СуммаДтБУ = СуммаДтБУ - Выборка.СуммаОборотДтБУ;
							КонецЕсли;
						КонецЕсли;
					Иначе
						РазницаМеждуДтКт = Выборка.СуммаОборотДтБУ - Выборка.СуммаОборотКтБУ;
						
						СформироватьПроводку(ДокументОперацияБух, Выборка, РазницаМеждуДтКт * (-1),
						 - Выборка.СуммаОборотДтНУ, Выборка.СуммаОборотДтБУ + РазницаМеждуДтКт * (-1) - Выборка.СуммаОборотКтБУ, 
						 СтруктураЗначений);
						СуммаДтБУ = СуммаДтБУ - (Выборка.СуммаОборотДтБУ - РазницаМеждуДтКт);
						ЗаписьПеременных(Выборка);
						
					КонецЕсли;
				КонецЦикла;
			
		
			Если СуммаДтБУ > 0 И Выборка <> Неопределено Тогда
				
				Проводка = ДокументОперацияБух.Движения.Хозрасчетный.Добавить();
				Проводка.Период = КонецМесяца(Дата); 
				Проводка.Регистратор = ДокументОперацияБух.Ссылка;
				Проводка.Организация = Организация;
						
				Проводка.СчетДт = ВыполненныеЭтапы;
				Проводка.СубконтоДт.Договоры = Договор;
				Проводка.СубконтоДт.Контрагенты = Контрагент;
				Проводка.СубконтоДт.НоменклатурныеГруппы = НоменклатурнаяГруппа;
						
				Проводка.СчетКт = ВыручкаНеОблЕНВД;
				Проводка.СубконтоКт.НоменклатурныеГруппы = НоменклатурнаяГруппа;
				Проводка.СубконтоКт.СтавкиНДС = НДС18;	
	
				Проводка.Содержание = СтруктураЗначений.Содержание;
				Проводка.Сумма = СуммаДтБУ;
				Проводка.СуммаВРДт = - СуммаОборотДтНУ;
				Проводка.СуммаВРКт = СуммаОборотДтБУ + СуммаДтБУ - СуммаОборотКтБУ;
				
			КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	// } </ИГС>
		
	// Дополнительная проверка на случай, если не удалось найти закрытые проекты, требующие перераспределения
	// В таком случае нет необходимости менять документ/создавать новый
	Если ДокументОперацияБух.Движения.Хозрасчетный.Количество() > 0 Тогда
		
		ДокументОперацияБух.Записать(РежимЗаписиДокумента.Запись);
		Возврат ДокументОперацияБух.Ссылка;
		
	Иначе
		
		Возврат ПредопределенноеЗначение("Документ.ОперацияБух.ПустаяСсылка");
		
	КонецЕсли;	
	
КонецФункции

Процедура СформироватьПроводку(ОперацияБух, Выборка, Сумма, СуммаВРДт, СуммаВРКт, СтруктураЗначений)
		
	Проводка = ОперацияБух.Движения.Хозрасчетный.Добавить();
	Проводка.Период = КонецМесяца(Дата); 
	Проводка.Регистратор = ОперацияБух.Ссылка;
	Проводка.Организация = Организация;
						
	Проводка.СчетДт = СтруктураЗначений.ВыполненныеЭтапы;
	Проводка.СубконтоДт.Договоры = Выборка.Договор;
	Проводка.СубконтоДт.Контрагенты = Выборка.Контрагент;
	Проводка.СубконтоДт.НоменклатурныеГруппы = Выборка.НоменклатурнаяГруппа;
						
	Проводка.СчетКт = СтруктураЗначений.ВыручкаНеОблЕНВД;
	Проводка.СубконтоКт.НоменклатурныеГруппы = Выборка.НоменклатурнаяГруппа;
	Проводка.СубконтоКт.СтавкиНДС = СтруктураЗначений.НДС18;	
	
	Проводка.Содержание = "Перераспределение 46 счета";
	Проводка.Сумма = Сумма;
	Проводка.СуммаВРДт = СуммаВРДт;
	Проводка.СуммаВРКт = СуммаВРКт;
	
КонецПроцедуры

Процедура ЗаписьПеременных (Выборка)
	
	Контрагент = Выборка.Контрагент;
	Договор = Выборка.Договор;
	НоменклатурнаяГруппа = Выборка.НоменклатурнаяГруппа;
	СуммаОборотДтБУ = Выборка.СуммаОборотДтБУ;
	СуммаОборотКтБУ = Выборка.СуммаОборотКтБУ; 
	СуммаОборотДтНУ = Выборка.СуммаОборотДтНУ;
	
КонецПроцедуры

#КонецОбласти  
