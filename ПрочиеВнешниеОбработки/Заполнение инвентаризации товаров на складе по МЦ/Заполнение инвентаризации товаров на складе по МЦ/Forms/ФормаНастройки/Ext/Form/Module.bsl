
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПолучитьСчетаДляСпискаВыбора(); 
	
КонецПроцедуры 

&НаСервере
Процедура ПолучитьСчетаДляСпискаВыбора()
	
	СтрокаСчетов = "10.01 10.02 10.03.01 10.04 10.05 10.06 10.07 10.08 10.09 10.10 10.15 10.16 10.17 10.21 10.НП 41.01 08.04 07 002 003.01 003.02 МЦ.02 МЦ.03 МЦ.04.А МЦ.06 МЦ.10"; 
	
	МассивПоиска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСчетов, " ");
	
	ЗапросСчетов = Новый Запрос;
	ЗапросСчетов.Текст = 
	"ВЫБРАТЬ
	|	Хозрасчетный.Ссылка КАК Счет,
	|	Хозрасчетный.Код + "" "" + Хозрасчетный.Наименование КАК Представление
	|ИЗ
	|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
	|ГДЕ
	|	Хозрасчетный.Код В(&СписокКодов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Счет ИЕРАРХИЯ
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	ЗапросСчетов.УстановитьПараметр("СписокКодов", МассивПоиска);
	
	РезультатЗапроса = ЗапросСчетов.Выполнить(); 
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		СписокЗапроса = РезультатЗапроса.Выгрузить();
		
		СписокСчетов.Загрузить(СписокЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЧТоварыНаСервере()
	
	СчетаДляЗапроса = СписокСчетов.Выгрузить(Новый Структура("Использовать", Истина)); 
	
	ОрганизацияДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.СсылкаНаОбъект, "Организация");
	
	СубконтоНоменклатурныеГруппыМассив = Новый Массив;
	СубконтоНоменклатурныеГруппыМассив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	СубконтоНоменклатурныеГруппыМассив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НоменклатурныеГруппы);
	
	СубконтоФизЛицМассив = Новый Массив;
	СубконтоФизЛицМассив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	СубконтоФизЛицМассив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РаботникиОрганизаций); 
	
	СубконтоКонтагентовМассив = Новый Массив;
	СубконтоКонтагентовМассив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	СубконтоКонтагентовМассив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты); 
	
	СубконтоСкладМассив = Новый Массив;
	СубконтоСкладМассив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	СубконтоСкладМассив.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады); 
	
	Счета002и00301 = Новый Массив;
	Счета002и00301.Добавить(ПланыСчетов.Хозрасчетный.ТМЦпринятыеНаОтветственноеХранение);
	Счета002и00301.Добавить(ПланыСчетов.Хозрасчетный.МатериалыПринятыеВПереработку);
	
	Субконто002 = Новый Массив;
	Субконто002.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	Субконто002.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Контрагенты);
	Субконто002.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);
	
	ЗапросТоваров = Новый Запрос;
	ЗапросТоваров.УстановитьПараметр("ДатаРасчета", 					Объект.Период);
	ЗапросТоваров.УстановитьПараметр("СписокСчетов", 					СчетаДляЗапроса);	
	ЗапросТоваров.УстановитьПараметр("СубконтоНоменклатурныеГруппы", 	СубконтоНоменклатурныеГруппыМассив);
	ЗапросТоваров.УстановитьПараметр("СубконтоФизЛиц", 					СубконтоФизЛицМассив);
	ЗапросТоваров.УстановитьПараметр("СубконтоКонтрагентов", 			СубконтоКонтагентовМассив); 
	ЗапросТоваров.УстановитьПараметр("СубконтоСклад", 					СубконтоСкладМассив);
	ЗапросТоваров.УстановитьПараметр("Склад", 							Объект.Склад);
	ЗапросТоваров.УстановитьПараметр("Организация", 					ОрганизацияДокумента);	
	ЗапросТоваров.УстановитьПараметр("Счета002и00301", 					Счета002и00301); 
	ЗапросТоваров.УстановитьПараметр("Контрагент", 						Объект.Контрагент);
	ЗапросТоваров.УстановитьПараметр("СубконтоСклад002", 				Субконто002);
	ЗапросТоваров.УстановитьПараметр("ФизЛицо", 						РаботникОрганизации); 
	ЗапросТоваров.УстановитьПараметр("НоменклатурнаяГруппа", 			Объект.НоменклатурнаяГруппа);

	ЗапросТоваров.Текст = 
	"ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ХозрасчетныйОстатки.СуммаОстатокДт КАК СуммаОстатокДт,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиПоСкладам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаРасчета,
	|			Счет В ИЕРАРХИИ (&СписокСчетов)
	|				И НЕ Счет В (&Счета002и00301),
	|			&СубконтоСклад,
	|			ВЫБОР
	|				КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ Организация = &Организация
	|			КОНЕЦ) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиПоСкладам002_003_02
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаРасчета,
	|			Счет В ИЕРАРХИИ (&СписокСчетов)
	|				И счет В (&Счета002и00301),
	|			&СубконтоСклад002,
	|			ВЫБОР
	|				КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА (Субконто2 = &Склад
	|							ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|							И (Субконто3 = &Контрагент
	|								ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|				ИНАЧЕ Организация = &Организация
	|						И ((Субконто2 = &Склад
	|							ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|							И (Субконто3 = &Контрагент
	|								ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)))
	|			КОНЕЦ) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	(ХозрасчетныйОстатки.Субконто2 = &Склад
	|			ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	И (ХозрасчетныйОстатки.Субконто3 = &Контрагент
	|			ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиПофизлицам
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаРасчета,
	|			Счет В ИЕРАРХИИ (&СписокСчетов)
	|				И НЕ Счет В (&Счета002и00301),
	|			&СубконтоФизЛиц,
	|			ВЫБОР
	|				КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ Организация = &Организация
	|			КОНЕЦ) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	(ХозрасчетныйОстатки.Субконто2 = &ФизЛицо
	|			ИЛИ &Физлицо = ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиКонтрагентов
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаРасчета,
	|			Счет В ИЕРАРХИИ (&СписокСчетов)
	|				И НЕ счет В (&Счета002и00301),
	|			&СубконтоКонтрагентов,
	|			ВЫБОР
	|				КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ Организация = &Организация
	|			КОНЕЦ) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	(ХозрасчетныйОстатки.Субконто2 = &Контрагент
	|			ИЛИ &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто1,
	|	ХозрасчетныйОстатки.Субконто2 КАК Субконто2,
	|	ХозрасчетныйОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ВТ_ОстаткиНоменклатурныеГруппы
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&ДатаРасчета,
	|			Счет В ИЕРАРХИИ (&СписокСчетов)
	|				И НЕ счет В (&Счета002и00301),
	|			&СубконтоНоменклатурныеГруппы,
	|			ВЫБОР
	|				КОГДА &Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ Организация = &Организация
	|			КОНЕЦ) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	(ХозрасчетныйОстатки.Субконто2 = &НоменклатурнаяГруппа
	|			ИЛИ &НоменклатурнаяГруппа = ЗНАЧЕНИЕ(Справочник.НоменклатурныеГруппы.ПустаяСсылка))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОстаткиПоСкладам.Счет КАК Счет,
	|	ВТ_ОстаткиПоСкладам.Субконто1 КАК Субконто1,
	|	ВТ_ОстаткиПоСкладам.КоличествоОстаток КАК КоличествоОстаток,
	|	ВТ_ОстаткиПоСкладам.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	ВТ_ОстаткиПоСкладам КАК ВТ_ОстаткиПоСкладам
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВТ_ОстаткиПоСкладам002.Счет,
	|	ВТ_ОстаткиПоСкладам002.Субконто1,
	|	ВТ_ОстаткиПоСкладам002.КоличествоОстаток,
	|	ВТ_ОстаткиПоСкладам002.СуммаОстаток
	|ИЗ
	|	ВТ_ОстаткиПоСкладам002_003_02 КАК ВТ_ОстаткиПоСкладам002
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВТ_ОстаткиПофизлицам.Счет,
	|	ВТ_ОстаткиПофизлицам.Субконто1,
	|	ВТ_ОстаткиПофизлицам.КоличествоОстаток,
	|	ВТ_ОстаткиПофизлицам.СуммаОстаток
	|ИЗ
	|	ВТ_ОстаткиПофизлицам КАК ВТ_ОстаткиПофизлицам
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВТ_ОстаткиКонтрагентов.Счет,
	|	ВТ_ОстаткиКонтрагентов.Субконто1,
	|	ВТ_ОстаткиКонтрагентов.КоличествоОстаток,
	|	ВТ_ОстаткиКонтрагентов.СуммаОстаток
	|ИЗ
	|	ВТ_ОстаткиКонтрагентов КАК ВТ_ОстаткиКонтрагентов
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ВТ_ОстаткиНоменклатурныеГруппы.Счет,
	|	ВТ_ОстаткиНоменклатурныеГруппы.Субконто1,
	|	ВТ_ОстаткиНоменклатурныеГруппы.КоличествоОстаток,
	|	ВТ_ОстаткиНоменклатурныеГруппы.СуммаОстаток
	|ИЗ
	|	ВТ_ОстаткиНоменклатурныеГруппы КАК ВТ_ОстаткиНоменклатурныеГруппы";
	
    РезультатЗапроса = ЗапросТоваров.Выполнить(); 
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		
		ЦелевойОбъект = Объект.СсылкаНаОбъект.ПолучитьОбъект();
		ЦелевойОбъект.Товары.Очистить();
		ТоварыДляЗаполнения = РезультатЗапроса.Выгрузить(); 
		
		Для Каждого Товар Из ТоварыДляЗаполнения Цикл
			
			НоваяСтрока 				= ЦелевойОбъект.Товары.Добавить();
			НоваяСтрока.Номенклатура 	= Товар.Субконто1;
			НоваяСтрока.Количество 		= Товар.КоличествоОстаток;
			НоваяСтрока.КоличествоУчет 	= Товар.КоличествоОстаток;
			НоваяСтрока.СчетУчета 		= Товар.Счет;
			НоваяСтрока.СуммаУчет 		= Товар.СуммаОстаток;
			НоваяСтрока.Сумма 			= Товар.СуммаОстаток; 
			
			Если Товар.КоличествоОстаток = 0 Тогда 
				НоваяСтрока.Цена = 0;
			Иначе 
				НоваяСтрока.Цена = Товар.СуммаОстаток / Товар.КоличествоОстаток; 
			КонецЕсли;
			
		КонецЦикла;
		
		Если СворачиватьПоНоменклатурнымГруппам Тогда
			
			ЦелевойОбъект.Товары.Свернуть("СчетУчета, Номенклатура","Количество, КоличествоУчет, Сумма, СуммаУчет"); 
			
			Для Каждого Товар Из ЦелевойОбъект.Товары Цикл 
				
				Если Товар.Количество = 0 Тогда 
					Товар.Цена = 0;
				Иначе 					
					Товар.Цена = Товар.СуммаУчет / Товар.КоличествоУчет;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ЦелевойОбъект.Записать(); 
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТЧТовары(Команда)
	
	ЗаполнитьТЧТоварыНаСервере();
	
	Если ВладелецФормы <> Неопределено Тогда
		ВладелецФормы.Прочитать();
	КонецЕсли;   

	ЭтотОбъект.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого СтрокаОбъектовНазначения Из Параметры.ОбъектыНазначения Цикл 
		Документ = СтрокаОбъектовНазначения.Ссылка;
	КонецЦикла;
	
	Объект.Период 			= Документ.Дата;
	Объект.Склад 			= Документ.Склад;
	Объект.Контрагент 		= Документ.игсКонтрагент;
	Объект.СсылкаНаОбъект 	= Документ;
	
КонецПроцедуры
