
Функция СведенияОВнешнейОбработке() Экспорт 
	
	ПараметрыРегистрации 					= ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке("2.3.3.0");
	ПараметрыРегистрации.Информация 		= НСтр("ru = 'Обработка заполнения табличной части ОС документа ИнвентаризацияОС по выбранному счету.'");
	ПараметрыРегистрации.Наименование 		= НСтр("ru = 'Заполнение ОС по счету 01/001/МЦ.08'");
	ПараметрыРегистрации.Вид 				= ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиЗаполнениеОбъекта();
	ПараметрыРегистрации.Версия 			= "1.1";
	ПараметрыРегистрации.БезопасныйРежим 	= Ложь;
	ПараметрыРегистрации.Назначение.Добавить("Документ.ИнвентаризацияОС");
	
	Команда 								= ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление 					= НСтр("ru = 'Заполнить по 01'");
	Команда.Идентификатор 					= "ЗаполнитьПо01";
	Команда.Использование 					= ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыЗаполнениеФормы();
	Команда.ПоказыватьОповещение 			= Ложь;
	
	Команда 								= ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление 					= НСтр("ru = 'Заполнить по 001'");
	Команда.Идентификатор 					= "ЗаполнитьПо001";
	Команда.Использование 					= ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыЗаполнениеФормы();
	Команда.ПоказыватьОповещение 			= Ложь;
	
	Команда 								= ПараметрыРегистрации.Команды.Добавить();
	Команда.Представление 					= НСтр("ru = 'Заполнить по МЦ.08'");
	Команда.Идентификатор 					= "ЗаполнитьПоМЦ08";
	Команда.Использование 					= ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыЗаполнениеФормы();
	Команда.ПоказыватьОповещение 			= Ложь;
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура ВыполнитьКоманду(ИдентификаторКомады, ОбъектыНазначения, ПараметрыВыполнения) Экспорт 
	
	МассивСчетов = Новый Массив;
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.АмортизацияОсновныхСредств);
	МассивСчетов.Добавить(ПланыСчетов.Хозрасчетный.ДоходныеВложенияВ_МЦ);
	
	Счет001 	= ПланыСчетов.Хозрасчетный.АрендованныеОсновныеСредства; 
	СчетМЦ08 	= ПланыСчетов.Хозрасчетный.игсБуровыеИКомплектующие;
	
	Если ИдентификаторКомады 		= "ЗаполнитьПо01" Тогда 
		ЗаполнитьТабличнуюЧастьОС(ПараметрыВыполнения.ЭтаФорма, МассивСчетов);
	ИначеЕсли ИдентификаторКомады 	= "ЗаполнитьПо001" Тогда 
		ЗаполнитьТабличнуюЧастьОС(ПараметрыВыполнения.ЭтаФорма, Счет001);
	ИначеЕсли ИдентификаторКомады 	= "ЗаполнитьПоМЦ08" Тогда 
		ЗаполнитьТабличнуюЧастьОС(ПараметрыВыполнения.ЭтаФорма, СчетМЦ08);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТабличнуюЧастьОС(Форма, Счет)

	Форма.Объект.ОС.Очистить();
	
	ПорядокСубконто = Новый Массив;
	ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	игсОстаткиОСНаСкладахОстатки.Организация КАК Организация,
		|	игсОстаткиОСНаСкладахОстатки.ОС КАК ОС,
		|	игсОстаткиОСНаСкладахОстатки.Склад КАК Склад,
		|	игсОстаткиОСНаСкладахОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ПОМЕСТИТЬ ОстаткиОС
		|ИЗ
		|	РегистрНакопления.игсОстаткиОСНаСкладах.Остатки(
		|			&ДатаДокумента,
		|			Организация = &Организация
		|				И Склад = &Склад) КАК игсОстаткиОСНаСкладахОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстатки.Субконто1 КАК ОС,
		|	ХозрасчетныйОстатки.СуммаОстаток КАК СуммаОстаток
		|ПОМЕСТИТЬ ХозрасчетныйОстатки
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(
		|			&ДатаДокумента,
		|			Счет В ИЕРАРХИИ (&Счет),
		|			&ПорядокСубконто,
		|			Субконто1 В
		|				(ВЫБРАТЬ
		|					ОстаткиОС.ОС КАК ОС
		|				ИЗ
		|					ОстаткиОС КАК ОстаткиОС)) КАК ХозрасчетныйОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйОстатки.ОС КАК ОсновноеСредство,
		|	ОсновныеСредства.Код КАК ИнвентарныйНомер,
		|	ЕСТЬNULL(ОстаткиОС.КоличествоОстаток, 0) КАК КоличествоПоДаннымУчета,
		|	СУММА(ЕСТЬNULL(ХозрасчетныйОстатки.СуммаОстаток, 0)) КАК СтоимостьПоРезультатамИнвентаризации
		|ИЗ
		|	ХозрасчетныйОстатки КАК ХозрасчетныйОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОстаткиОС КАК ОстаткиОС
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
		|			ПО ОстаткиОС.ОС = ОсновныеСредства.Ссылка
		|		ПО (ОстаткиОС.ОС = ХозрасчетныйОстатки.ОС)
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйОстатки.ОС,
		|	ОсновныеСредства.Код,
		|	ЕСТЬNULL(ОстаткиОС.КоличествоОстаток, 0)";
	
	Запрос.УстановитьПараметр("ДатаДокумента", 		Форма.Объект.Дата);
	Запрос.УстановитьПараметр("Организация", 		Форма.Объект.Организация);
	Запрос.УстановитьПараметр("ПорядокСубконто", 	ПорядокСубконто);
	Запрос.УстановитьПараметр("Склад", 				Форма.Объект.игсСклад);
	Запрос.УстановитьПараметр("Счет", 				Счет);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОС = РезультатЗапроса.Выгрузить();
	
	Форма.Объект.ОС.Загрузить(ВыборкаОС); 
	
	Форма.Модифицированность = Истина;
	
КонецПроцедуры

