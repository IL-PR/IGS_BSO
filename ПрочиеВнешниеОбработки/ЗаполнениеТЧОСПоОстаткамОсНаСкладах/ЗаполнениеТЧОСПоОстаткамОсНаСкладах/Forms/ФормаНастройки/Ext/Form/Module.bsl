
&НаСервере
Функция ЗапросОС() 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	игсОстаткиОСНаСкладахОстатки.ОС КАК ОС
		|ИЗ
		|	РегистрНакопления.игсОстаткиОСНаСкладах.Остатки(
		|			&ДатаГраница,
		|			Склад = &Склад
		|				И Организация = &Организация) КАК игсОстаткиОСНаСкладахОстатки"; 
	
	Если Объект.ОрганизацияОтбор.Пустая() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Организация = &Организация","Истина");
	Иначе
		Запрос.УстановитьПараметр("Организация", Объект.ОрганизацияОтбор);
	КонецЕсли;    
	
	Если Объект.СкладОтбор.Пустая() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Склад = &Склад","Истина");
	Иначе
		Запрос.УстановитьПараметр("Склад", Объект.СкладОтбор);
	КонецЕсли; 
	
	Если Объект.ДатаОтбор = Дата('00010101') Тогда
		Запрос.УстановитьПараметр("ДатаГраница", новый Граница(ТекущаяДатаСеанса(), ВидГраницы.Включая));
	Иначе
		Запрос.УстановитьПараметр("ДатаГраница", Новый Граница(Объект.ДатаОтбор, ВидГраницы.Включая));
	КонецЕсли;
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Выгрузить();
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьКоманду(ИдентификаторКоманды, ОбъектыНазначенияМассив) Экспорт    
	
	Объект.СсылкаНаОбъект 	= ВладелецФормы.Объект.Ссылка;
	ОбъектЗаполнения 		= Объект.СсылкаНаОбъект;
	ЗакрытиеФормы 			= Новый ОписаниеОповещения("ЗаполнениеПоВыбраннымПараметрам", ЭтаФорма);   
	
	ОрганизацияЗаполнена 	= ЗначениеЗаполнено(ВладелецФормы.Объект.Организация);
	
	Если Не ОрганизацияЗаполнена Тогда   
		
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнено поле ""Организация"" !"); 
		
		Возврат;
		
	КонецЕсли;
	
	Объект.ОрганизацияОтбор = ВладелецФормы.Объект.Организация;
	
	ЭтоПеремещение 			= ТипЗнч(Объект.СсылкаНаОбъект) = Тип("ДокументСсылка.ПеремещениеОС");
	ЭтоИзменениеОС 			= ТипЗнч(Объект.СсылкаНаОбъект) = Тип("ДокументСсылка.ИзменениеСостоянияОС");
	
	Если ЭтоПеремещение Тогда  
		
		Если ЗначениеЗаполнено(ВладелецФормы.Объект.игсСкладОтправитель) Тогда
			
			Объект.СкладОтбор = ВладелецФормы.Объект.игсСкладОтправитель;
			ЗаполнениеПоВыбраннымПараметрам(Истина, Истина); 
			
		Иначе 
			
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнено поле ""Склад"" !"); 
			
			Возврат; 
			
		КонецЕсли;
		
	ИначеЕсли ЭтоИзменениеОС Тогда   
		
		ОткрытьФорму("ВнешняяОбработка.ЗаполнениеТЧОСПоОстаткамОсНаСкладах.Форма.ЗапросПараметров", , 
		ОбъектЗаполнения,,,, ЗакрытиеФормы, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли; 
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнениеПоВыбраннымПараметрам(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат; 									// Пользователь не стал указывать параметры, останавливаем работу
	КонецЕсли;     
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда 
		Объект.СкладОтбор = Результат.СкладОтбор; 
	КонецЕсли;
	
	ЗаполнениеНаСервере(); 	  
	
	Если НЕ ВладелецФормы = Неопределено Тогда
		ВладелецФормы.Прочитать(); 					
	КонецЕсли; 
	
КонецПроцедуры  

&НаСервере
Процедура ЗаполнениеНаСервере()
	
	Документ = Объект.СсылкаНаОбъект.ПолучитьОбъект();
	
	ДанныеОС = ЗапросОС(); 
	
	Документ.ОС.Очистить();    
	
	Для Каждого СтрокаДанныхОс Из ДанныеОС Цикл   
		
		СтрокаДокументаОс = Документ.ОС.Добавить();
		СтрокаДокументаОс.ОсновноеСредство = СтрокаДанныхОс.Ос; 
		
	КонецЦикла;	
	
	Документ.Записать(); // Сохраняем результат   
	
КонецПроцедуры

